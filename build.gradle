//buildscript {
//    dependencies {
//        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4"
//    }
//}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'java-library'
    id 'org.jetbrains.dokka' version '0.10.0'
    id "com.jfrog.artifactory" version "4.10.0"
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
}

group 'de.xeroli.kbool'
version '0.2.0' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

ext {
    ext.snapshotversion = '0.2.0'
    ext.stableversion = '0.1.0'

    bintrayUser = System.getProperty("bintray.user")
    bintrayKey = System.getProperty("bintray.key")
    buildNumber = System.getProperty("build.number")
}

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    // options.encoding = 'UTF-8'
}

artifacts {
    archives sourceJar
    archives javadocJar
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"
}

task replaceVersionInREADME {
    doLast() {
        // Maven
        ant.replaceregexp(match: '<version>([0-9\\.]+)</version>', replace: "<version>${project.ext.stableversion}</version>", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        // Gradle
        ant.replaceregexp(match: 'de\\.xeroli\\.kbool\\:kbool\\:([0-9\\.]+)\'', replace: "de.xeroli.kbool:kbool:${project.ext.stableversion}'", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        // Maven
        ant.replaceregexp(match: '<version>([0-9\\.]+)-SNAPSHOT</version>', replace: "<version>${project.ext.snapshotversion}-SNAPSHOT</version>", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
        // Gradle
        ant.replaceregexp(match: 'de\\.xeroli\\.kbool\\:kbool\\:([0-9\\.]+)-SNAPSHOT', replace: "de.xeroli.kbool:kbool:${project.ext.snapshotversion}-SNAPSHOT", flags: 'g', byline: true) {
            fileset(dir: '.', includes: 'README.md')
        }
    }
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "xeroli"
            name "Roland Fischer"
            email "fischer@xeroli.de"
        }
    }

    scm {
        url "https://github.com/xeroli/kbool"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'de.xeroli.kbool'
            artifactId 'kbool'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', ' A simple kotlin library providing a transparent boolean algebra.')
                root.appendNode('name', 'KBool')
                root.appendNode('url', 'https://github.com/xeroli/kbool')
                root.children().last() + pomConfig
            }
        }
    }
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = bintrayUser
            password = bintrayKey
        }
        defaults {
            publications('mavenPublication')
            publishArtifacts = true
            publishPom = true
            properties = [
                    'build.number': buildNumber,
                    'build.name'  : 'kbool'
            ]
        }
    }
    resolve {
        repoKey = 'jcenter'
    }
    clientConfig.info.setBuildNumber(buildNumber)
    clientConfig.info.setBuildName('kbool')
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'kbool'
        userOrg = 'xeroli'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/xeroli/kbool.git'
        version {
            name = project.version
            desc = "build ${buildNumber}"
            released = new Date()
            gpg {
                sign = true
            }
        }
    }

    publish = true
}
